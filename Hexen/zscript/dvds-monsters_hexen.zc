
// Pig
class PigNew : AetheriusMorphedMonster replaces Pig
{
	default
	{
		Tag "Pig";
		Health 25;
		Painchance 128;
		Painchance "Fist", 192; // Fist attacks have a +50% chance of stunning an enemy
		Painchance "Kick", 255; // Kick attacks have a +150% chance of stunning an enemy
		Painchance "Gauntlets", 192; // Fist attacks have a +50% chance of stunning an enemy
		DamageFactor 0.5;
		RadiusDamageFactor 0.5;
		DamageFactor "Explosion", 0.5;
		DamageFactor "Crush", 4.0;
		Speed 10;
		Radius 12;
		Height 22;
		Mass 60;
		Monster;
		-COUNTKILL;
		+WINDTHRUST;
		+DONTMORPH;
		SeeSound "PigActive1";
		PainSound "PigPain";
		DeathSound "PigDeath";
		ActiveSound "PigActive1";
	}
	
	States
	{
		Spawn:
			PIGY B 0 NoDelay;
			PIGY B 10 A_Look();
			goto Spawn+1;
		See:
			PIGY A 0;
			PIGY ABCD 3
			{
				if (levelbehavior && user_monsterlevel > 90) { A_SetTics(2); }
				A_DVDSChase("Melee","Missile",0,1,3,1,1);
			}
			Loop;
		Melee:
			TNT1 A 0;
			PIGY A 5 A_FaceTarget();
			PIGY A 10 
			{
				A_FaceTarget();;
				
				A_CrisisMeleeAttack(random[PigAttack](2, 3), "PigAttack");
			}
			Goto See;
		Pain:
			PIGY D 4 A_PigPain();
			Goto See;
		Death:
			TNT1 A 0 A_DVDSNormalDeath(-2,0,1);
			PIGY E 4 A_Scream();
			PIGY F 3 A_NoBlocking();
			PIGY G 4 A_QueueCorpse();
			PIGY H 3;
			PIGY IJK 4; 
			PIGY L -1;
			Stop;
		Ice:
			#### # 0 A_DVDSIceDeath(-2,0,1);
			PIGY M 5 A_FreezeDeath();
			PIGY M 1 A_FreezeDeathChunks();
			Wait;
	}
}
class FPig : PigNew
{
	default
	{
		-COUNTKILL;
		+FRIENDLY;
		+THRUSPECIES;
		+DONTHARMSPECIES;
		Species "FriendlyMonsters";
		dropitem "NoItemSpawn", 1;
	}
}

